#
# A single line theme with Git information on the left and Ruby on the right.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#
# Screenshots:
#   http://i.imgur.com/gLgVp6Y.png
#

function prompt_skye_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

}

function prompt_skye_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)
  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_skye_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    if type skyescheme > /dev/null; then
      _prompt_skye_colors=(
        "%F{$(skyescheme cyan_accent)}"  # #62d6e8 # Turquoise
        "%F{$(skyescheme foreground)}" # White
        "%F{$(skyescheme warn)}" # Orange
        "%F{$(skyescheme accent)}" # Hotpink
        "%F{$(skyescheme correct)}" # Limegreen
        "%F{$(skyescheme purple_accent)}" # Purple
        "%F{$(skyescheme foreground_faded)}" # Faded
        "%F{$(skyescheme error)}" # red
      )
    elif [[ $(xrdb -q color1 2> /dev/null) ]]; then
      _prompt_skye_colors=(
        "%F{$(xrdb -get color20)}" #62d6e8 # Turquoise
        "%F{$(xrdb -get foreground)}" # White
        "%F{$(xrdb -get color11)}" # Orange
        "%F{$(xrdb -get color5)}" # Hotpink
        "%F{$(xrdb -get color2)}" # Limegreen
        "%F{$(xrdb -get color4)}" # Purple
        "%F{$(xrdb -get color8)}" # Faded
        "%F{$(xrdb -get color9)}" # Error
      )
    else
      _prompt_skye_colors=(
        "%F{#62d6e8}"  # Turquoise
        "%F{#e9e9f4}" # White
        "%F{#ebff87}" # Orange
        "%F{#ea51b2}" # Hotpink
        "%F{#00f769}" # Limegreen
        "%F{#b45bcf}" # Purple
        "%F{#626483}" # Faded
        "%F{#ff5555}" # Faded
      )
    fi
  else
    _prompt_skye_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
      "%F{purple}"
      "%F{white}"
      "%F{red}"
    )
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${_prompt_skye_colors[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${_prompt_skye_colors[5]}●%f"
  zstyle ':prezto:module:git:info:deleted' format "${_prompt_skye_colors[3]}●%f"
  zstyle ':prezto:module:git:info:modified' format "${_prompt_skye_colors[3]}●%f"
  zstyle ':prezto:module:git:info:untracked' format "${_prompt_skye_colors[6]}●%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '[%v]'

  # Define prompts.
  PROMPT="${_prompt_skye_colors[7]}%n%f@${_prompt_skye_colors[4]}%m%f ${_prompt_skye_colors[5]}%~%f "'${git_info:+${(e)git_info[prompt]}}'"%(?..${_prompt_skye_colors[8]})$ ${_prompt_skye_colors[2]}"
  RPROMPT='%F{blue}${ruby_info[version]}'
}

prompt_skye_setup "$@"
# vim: ft=zsh
